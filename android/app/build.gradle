apply plugin: 'com.android.library'
apply from: 'maven-push.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.1'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14 -frtti -fcxx-exceptions"
            }
        }
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your APK.
            abiFilters 'armeabi-v7a'
        }


    }
    publishNonDefault true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "COGNITO_IDENTITY_POOL_ID", "***REMOVED***"
        }
        debug {
            buildConfigField "String", "COGNITO_IDENTITY_POOL_ID", "***REMOVED***"
        }
    }

    // If the flavors are updated, make sure to update ViroView.java's static flavor fields.
    productFlavors {
        viro_core {
            buildConfigField "String", "dummyField", "\"BridgeOnly\""
        }

        viro_react {
            buildConfigField "String", "dummyField", "\"ViroReact\""
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':arcore_client')
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.google.android.exoplayer:exoplayer:r2.2.0'
    compile project(':libs:gvr')
    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'
    compile 'com.amazonaws:aws-android-sdk-core:2.2.+'
    compile 'com.amazonaws:aws-android-sdk-ddb:2.2.+'
    compile 'com.amazonaws:aws-android-sdk-ddb-mapper:2.2.+'
    compile 'com.amazonaws:aws-android-sdk-cognito:2.2.+'
    compile 'com.amazonaws:aws-android-sdk-cognitoidentityprovider:2.2.+'

}

task copyReleaseAAR(type: Copy) {
    from('build/outputs/aar')
    // Change the next line to reflect the path to your react-viro repo, this
    // script is run from ViroRenderer/android/app/
    into('../../../react-viro/android/viro_renderer/')
    include('app-viro_react-release.aar')
    rename('app-viro_react-release.aar', 'viro_renderer-release.aar')
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleRelease') {
        task.finalizedBy 'copyReleaseAAR'
    }
}

build.dependsOn(':extractNdk')
